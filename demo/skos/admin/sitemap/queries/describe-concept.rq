PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  foaf: <http://xmlns.com/foaf/0.1/>

DESCRIBE $this ?concept ?narrower ?broader ?related
WHERE
  { GRAPH ?graph
      { $this  ?p  ?o
        OPTIONAL
          { $this  foaf:primaryTopic  ?concept
            OPTIONAL
              {   { GRAPH ?narrowerGraph
                      { ?concept  skos:narrower  ?narrower }
                  }
                UNION
                  { GRAPH ?narrowerGraph
                      { ?narrower  skos:broader  ?concept }
                  }
              }
            OPTIONAL
              {   { GRAPH ?broaderGraph
                      { ?concept  skos:broader  ?broader }
                  }
                UNION
                  { GRAPH ?broaderGraph
                      { ?broader  skos:narrower  ?concept }
                  }
              }
            OPTIONAL
              {   { GRAPH ?relatedGraph
                      { ?concept  skos:related  ?related }
                  }
                UNION
                  { GRAPH ?relatedGraph
                      { ?related  skos:related  ?concept }
                  }
              }
          }
      }
  }