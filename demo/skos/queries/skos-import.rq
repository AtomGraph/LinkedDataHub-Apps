PREFIX dh:    <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:  <http://rdfs.org/sioc/ns#>
PREFIX foaf:  <http://xmlns.com/foaf/0.1/>
PREFIX skos:  <http://www.w3.org/2004/02/skos/core#>
PREFIX dct:   <http://purl.org/dc/terms/>

CONSTRUCT
{
    GRAPH ?item
    {
        ?thing ?p ?o .
        ?item a dh:Item ;
            sioc:has_container ?container ;
            dct:title ?prefLabel ;
            foaf:primaryTopic ?thing .
    }
}
WHERE
{
    SELECT *
    {
        {
            ?thing a skos:Concept
            BIND (uri(concat(str($base), "concepts/")) AS ?container)
        }
        UNION
        {
            ?thing a skos:ConceptScheme
            BIND (uri(concat(str($base), "concept-schemes/")) AS ?container)
        }
        BIND (uri(concat(str(?container), STRUUID(), "/")) AS ?item)
        ?thing ?p ?o
        OPTIONAL
        {
            ?thing skos:prefLabel ?prefLabel
            FILTER (langMatches(lang(?prefLabel), "en"))
        }
    }
}