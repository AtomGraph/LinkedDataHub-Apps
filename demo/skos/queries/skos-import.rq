PREFIX dh:    <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:  <http://rdfs.org/sioc/ns#>
PREFIX foaf:  <http://xmlns.com/foaf/0.1/>
PREFIX skos:  <http://www.w3.org/2004/02/skos/core#>
PREFIX dct:   <http://purl.org/dc/terms/>

CONSTRUCT
{
    GRAPH ?subjItem
    {
        ?newSubj ?p ?newObj .
        ?subjItem a dh:Item ;
            sioc:has_container ?subjContainer ;
            dct:title ?prefLabel ;
            foaf:primaryTopic ?newSubj .
    }
}
WHERE
  { SELECT  *
    WHERE
      {
        # rewrite subject URIs

           { ?s  a  skos:Concept
            BIND(URI(concat(str($base), "concepts/")) AS ?subjContainer)
            BIND(strafter(str(?s), "http://vocabularies.unesco.org/thesaurus/") AS ?subjId)
          }
        UNION
          { ?s  a  skos:Collection
            BIND(URI(concat(str($base), "collections/")) AS ?subjContainer)
            BIND(strafter(str(?s), "http://vocabularies.unesco.org/thesaurus/") AS ?subjId)
          }
        UNION
          { ?s  a  skos:ConceptScheme
            BIND(URI(concat(str($base), "concept-schemes/")) AS ?subjContainer)
            BIND("unesco-thesaurus" AS ?subjId)
          }
        BIND(URI(concat(str(?subjContainer), ?subjId, "/")) AS ?subjItem)
        BIND(URI(concat(str(?subjItem), "#this")) AS ?newSubj)

        ?s  ?p  ?o
        FILTER (?p != skos:hasTopConcept) # we'll display these relations using a view

        OPTIONAL
        {
            # rewrite object URIs

            { ?o  a  skos:Concept
                BIND(URI(concat(str($base), "concepts/")) AS ?objContainer)
                BIND(strafter(str(?o), "http://vocabularies.unesco.org/thesaurus/") AS ?objId)
            }
            UNION
            { ?o  a  skos:Collection
                BIND(URI(concat(str($base), "collections/")) AS ?objContainer)
                BIND(strafter(str(?o), "http://vocabularies.unesco.org/thesaurus/") AS ?objId)
            }
            UNION
            { ?o  a  skos:ConceptScheme
                BIND(URI(concat(str($base), "concept-schemes/")) AS ?objContainer)
                BIND("unesco-thesaurus" AS ?objId)
            }
            BIND(URI(concat(str(?objContainer), ?objId, "/")) AS ?objItem)
            BIND(URI(concat(str(?objItem), "#this")) AS ?newObjUri)
        }
        BIND (COALESCE(?newObjUri, ?o) AS ?newObj)
        
        OPTIONAL
          { ?s  skos:prefLabel  ?prefLabel
            FILTER langMatches(lang(?prefLabel), "en")
          }
      }
  }
