PREFIX dom:   <ns/domain#>
PREFIX dh:    <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:  <http://rdfs.org/sioc/ns#>
PREFIX foaf:  <http://xmlns.com/foaf/0.1/>
PREFIX skos:  <http://www.w3.org/2004/02/skos/core#>
PREFIX dct:   <http://purl.org/dc/terms/>

CONSTRUCT
{
    ?thing ?p ?o ;
        a ?type ;
        ?p ?o .
    ?item a ?itemType ;
        sioc:has_container ?container ;
        dh:slug ?id ;
        dct:title ?prefLabel ;
        foaf:primaryTopic ?thing .
}
WHERE
{
    SELECT *
    {
        {
            ?thing a skos:Concept
            BIND (dom:Concept AS ?type)
            BIND (dom:ConceptItem AS ?itemType)
            BIND (<concepts/> AS ?container)
        }
        UNION
        {
            ?thing a skos:ConceptScheme
            BIND (dom:ConceptScheme AS ?type)
            BIND (dom:ConceptSchemeItem AS ?itemType)
            BIND (<concept-schemes/> AS ?container)
        }
        BIND(struuid() AS ?id)
        BIND (BNODE() AS ?item)
        ?thing ?p ?o
        OPTIONAL
        {
            ?thing skos:prefLabel ?prefLabel
            FILTER (langMatches(lang(?prefLabel), "en"))
        }
    }
}