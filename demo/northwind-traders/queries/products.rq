PREFIX def:        <ns/default#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT
{
    ?item a def:Item ;
        sioc:has_container ?this ;
        dh:slug ?productID ;
        dct:title ?productName ;
        foaf:primaryTopic ?product .
    ?product a schema:Product ;
        foaf:isPrimaryTopicOf ?item ;
        schema:id ?productID ;
        schema:name ?productName ;
        schema:provider ?supplier ;
        schema:category ?category ;
        schema:description ?quantityPerUnit ;
        schema:price ?unitPrice .
        # ?unitsInStock ;
        # ?unitsOnOrder ;
        # ?reorderLevel ;
        # ?discontinued - schema:Discontinued
}
WHERE
{
    BIND (BNODE() AS ?item)

    ?product_row <#productID> ?productID ;
        <#productName> ?productName ;
        <#supplierID> ?supplierID ;
        <#categoryID> ?categoryID ;
        <#quantityPerUnit> ?quantityPerUnit ;
        <#unitPrice> ?unitPriceString ;
        <#unitsInStock> ?unitsInStockString ;
        <#unitsOnOrder> ?unitsOnOrderString ;
        <#reorderLevel> ?reorderLevelString ;
        <#discontinued> ?discontinuedString .

    BIND(uri(concat(str(<products/>), encode_for_uri(?productID), "/#this")) AS ?product)
    BIND(uri(concat(str(<suppliers/>), encode_for_uri(?supplierID), "/#this")) AS ?supplier)
    BIND(uri(concat(str(<categories/>), encode_for_uri(?categoryID), "/#this")) AS ?category)

    BIND (STRDT(?unitPriceString, xsd:float) AS ?unitPrice)
    BIND (STRDT(?unitsInStockString, xsd:integer) AS ?unitsInStock)
    BIND (STRDT(?unitsOnOrderString, xsd:integer) AS ?unitsOnOrder)
    BIND (STRDT(?reorderLevelString, xsd:integer) AS ?reorderLevel)
    BIND (STRDT(?discontinuedString, xsd:integer) AS ?discontinued)
}