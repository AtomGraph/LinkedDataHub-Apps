PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>

CONSTRUCT
{
    GRAPH ?graph
    {
        ?graph dct:title ?companyName ;
            foaf:primaryTopic ?shipper .

        ?shipper a schema:Corporation ;
            dct:title ?companyName ;
            schema:legalName ?companyName ;
            schema:identifier ?shipperID ;
            schema:telephone ?phone .
    }
}
WHERE
{
    ?shipper_row <#shipperID> ?shipperID ;
        <#companyName> ?companyName ;
        <#phone> ?phone .

    BIND(uri(concat(str($base), "shippers/")) AS ?container)
    BIND(uri(concat(str(?container), encode_for_uri(?shipperID), "/")) AS ?graph)
    BIND(uri(concat(str(?graph), "#this")) AS ?shipper)
}
