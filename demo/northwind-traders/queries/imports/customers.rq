PREFIX def:        <ns/default#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    ?item a def:Item ;
        sioc:has_container ?this ;
        dh:slug ?customerID ;
        dct:title ?companyName ;
        foaf:primaryTopic ?customer .

    ?customer a schema:Corporation ;
        foaf:isPrimaryTopicOf ?item ;
        schema:identifier ?customerID ;
        dct:title ?companyName ;
        schema:legalName ?companyName ;
        schema:contactPoint ?contactPoint ;
        schema:employee ?employee ;
        schema:address ?postalAddress .

    ?contactPoint a schema:ContactPoint ;
        schema:telephone ?phone ;
        schema:faxNumber ?fax .

    ?employee a schema:Person ;
        schema:name ?contactName ;
        dct:title ?contactName ;
        schema:jobTitle ?contactTitle ;
        schema:telephone ?phone ;
        schema:faxNumber ?fax .

    ?postalAddress a schema:PostalAddress ;
        schema:addressCountry ?country ;
        schema:addressLocality ?city ;
        schema:postalCode ?postalCode ;
        schema:streetAddress ?address ;
        schema:addressRegion ?region .
}
WHERE
{
    ?customer_row <#customerID> ?customerID ;
        <#companyName> ?companyName ;
        <#contactName> ?contactName ;
        <#contactTitle> ?contactTitle ;
        <#address> ?address ;
        <#city> ?city ;
        <#postalCode> ?postalCode ;
        <#country> ?country ;
        <#phone> ?phone .
    OPTIONAL {
        ?customer_row <#region> ?region
    }
    OPTIONAL {
        ?customer_row <#fax> ?fax
    }

    BIND(uri(concat(str(<customers/>), encode_for_uri(?customerID), "/")) AS ?item)
    BIND(uri(concat(str(<customers/>), encode_for_uri(?customerID), "/#this")) AS ?customer)
    BIND(uri(concat(str(<customers/>), encode_for_uri(?customerID), "/#contact-point")) AS ?contactPoint)
    BIND(uri(concat(str(<customers/>), encode_for_uri(?customerID), "/#employee")) AS ?employee)
    BIND(uri(concat(str(<customers/>), encode_for_uri(?customerID), "/#postal-address")) AS ?postalAddress)
}
