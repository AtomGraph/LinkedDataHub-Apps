PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    ?item a dh:Item ;
        sioc:has_container ?container ;
        dh:slug ?regionID ;
        dct:title ?regionDescription ;
        foaf:primaryTopic ?region .

    ?region a schema:Place ;
        dct:title ?regionDescription ;
        schema:name ?regionDescription ;
        schema:identifier ?regionID .
}
WHERE
{
    BIND (uri(concat(str($base), "regions/")) AS ?container)

    ?region_row <#regionID> ?regionID ;
        <#regionDescription> ?regionDescription.

    BIND(uri(concat(str(?container), encode_for_uri(?regionID), "/")) AS ?item)
    BIND(uri(concat(str(?item), "#this")) AS ?region)
}
