PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT
{
    GRAPH ?item
    {
        ?order schema:orderedItem ?orderItem .

        ?orderItem a schema:OrderItem ;
            schema:orderedItem ?product ;
            schema:orderQuantity ?quantity ;
            schema:discount ?discount ;
            schema:price ?price .
    }
}
WHERE
{
    ?order_detail_row <#orderID> ?orderID ;
        <#productID> ?productID ;
        <#unitPrice> ?unitPrice ;
        <#quantity> ?quantityString ;
        <#discount> ?discountString .

    BIND(uri(concat(str(<orders/>), encode_for_uri(?orderID))) AS ?item)
    BIND(uri(concat(str(?item), "/#this")) AS ?order)
    BIND(uri(concat(str(<orders/>), encode_for_uri(?orderID), "/#", STRUUID())) AS ?orderItem)
    BIND(uri(concat(str(<products/>), encode_for_uri(?productID), "/#this")) AS ?product)
    BIND (STRDT(?unitPrice, xsd:float) AS ?price)
    BIND (STRDT(?quantityString, xsd:integer) AS ?quantity)
    BIND (STRDT(?discountString, xsd:float) AS ?discount)
}
