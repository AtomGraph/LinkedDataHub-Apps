PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    ?item a dh:Item ;
        sioc:has_container ?this ;
        dh:slug ?territoryID ;
        dct:title ?territoryDescription ;
        foaf:primaryTopic ?territory .

    ?territory a schema:City ;
        foaf:isPrimaryTopicOf ?item ;
        dct:title ?territoryDescription ;
        schema:name ?territoryDescription ;
        schema:identifier ?territoryID ;
        schema:containedInPlace ?region .
}
WHERE
{
    ?territory_row <#territoryID> ?territoryID ;
        <#territoryDescription> ?territoryDescription ;
        <#regionID> ?regionID .

    BIND(uri(concat(str(<territories/>), encode_for_uri(?territoryID), "/")) AS ?item)
    BIND(uri(concat(str(<territories/>), encode_for_uri(?territoryID), "/#this")) AS ?territory)
    BIND(uri(concat(str(<regions/>), encode_for_uri(?regionID), "/#this")) AS ?region)
}
