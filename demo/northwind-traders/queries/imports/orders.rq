PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT
{
    ?item a dh:Item ;
        sioc:has_container ?container ;
        dh:slug ?orderID ;
        dct:title ?orderID ;
        foaf:primaryTopic ?order ;
        foaf:topic ?orderDelivery, ?deliveryAddress .

    ?order a schema:Order ;
        schema:identifier ?orderID ;
        dct:title ?orderID ;
        schema:customer ?customer ;
        schema:broker ?employee ;
        schema:orderDate ?orderDate ;
        schema:orderDelivery ?orderDelivery .

    ?orderDelivery a schema:ParcelDelivery ;
        foaf:page ?item ;
        schema:expectedArrivalUntil ?requiredDate ;
        # ?shippedDate ;
        schema:deliveryAddress ?deliveryAddress ;
        schema:provider ?shipper .

    ?deliveryAddress a schema:PostalAddress ;
        foaf:page ?item ;
        schema:addressCountry ?shipCountry ;
        schema:addressLocality ?shipCity ;
        schema:postalCode ?shipPostalCode ;
        schema:streetAddress ?shipAddress ;
        schema:location ?deliveryLocation ;
        schema:addressRegion ?shipRegion .
}
WHERE
{
    BIND (uri(concat(str($base), "orders/")) AS ?container)

    ?order_row <#orderID> ?orderID ;
        <#customerID> ?customerID ;
        <#employeeID> ?employeeID ;
        <#orderDate> ?orderDateString ;
        <#requiredDate> ?requiredDateString ;
        <#shipVia> ?shipVia ;
        <#freight> ?freightString ;
        <#shipName> ?shipName ;
        <#shipAddress> ?shipAddress ;
        <#shipCity> ?shipCity ;
        <#shipPostalCode> ?shipPostalCode ;
        <#shipCountry> ?shipCountry .

    OPTIONAL {
        ?order_row <#shippedDate> ?shippedDateString
        BIND(strdt(?shippedDateString, xsd:date) AS ?shippedDate)
    }
    OPTIONAL {
        ?order_row <#shipRegion> ?shipRegion
    }

    BIND(uri(concat(str(?container), encode_for_uri(?orderID), "/")) AS ?item)
    BIND(uri(concat(str(?item), "#this")) AS ?order)
    BIND(uri(concat(str(?item), "#delivery")) AS ?orderDelivery)
    BIND(uri(concat(str(?item), "#delivery-address")) AS ?deliveryAddress)
    BIND(uri(concat(str($base), "employees/", encode_for_uri(?employeeID), "/#this")) AS ?employee)
    BIND(uri(concat(str($base), "customers/", encode_for_uri(?customerID), "/#this")) AS ?customer)
    BIND(uri(concat(str($base), "shippers/", encode_for_uri(?shipVia), "/#this")) AS ?shipper)
    BIND(strdt(?orderDateString, xsd:date) AS ?orderDate)
    BIND(strdt(?requiredDateString, xsd:date) AS ?requiredDate)
    BIND(strdt(?freightString, xsd:float) AS ?freight)
}
