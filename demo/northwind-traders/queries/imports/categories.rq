PREFIX nsdd:       <ns/domain/default#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
  ?item a nsdd:Item ;
      sioc:has_container ?this ;
      dh:slug ?categoryID ;
      dct:title ?categoryName ;
      foaf:primaryTopic ?category .

  ?category a schema:ProductGroup ;
      foaf:isPrimaryTopicOf ?item ;
      dct:title ?categoryName ;
      dct:description ?description ;
      schema:name ?categoryName ;
      schema:identifier ?categoryID ;
      schema:description ?description ;
      foaf:depiction ?picture .
}
WHERE
{
    ?category_row <#categoryID> ?categoryID ;
        <#description> ?description ;
        <#categoryName> ?categoryName ;
        <#pictureHash> ?pictureHash .

    BIND(uri(concat(str(<categories/>), encode_for_uri(?categoryID), "/")) AS ?item)
    BIND(uri(concat(str(<categories/>), encode_for_uri(?categoryID), "/#this")) AS ?category)
    BIND(uri(concat(str(<uploads/>), encode_for_uri(?pictureHash), "/")) AS ?picture)
}
