PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT
{
    GRAPH ?item
    {
        ?item a dh:Item ;
            sioc:has_container ?container ;
            dh:slug ?productID ;
            dct:title ?productName ;
            foaf:primaryTopic ?product .

        ?product a schema:Product ;
            schema:identifier ?productID ;
            dct:title ?productName ;
            schema:name ?productName ;
            schema:provider ?supplier ;
            schema:category ?category ;
            schema:description ?quantityPerUnit ;
            schema:price ?unitPrice .
            # ?unitsInStock ;
            # ?unitsOnOrder ;
            # ?reorderLevel ;
            # ?discontinued - schema:Discontinued
    }
}
WHERE
{
    BIND (uri(concat(str($base), "products/")) AS ?container)

    ?product_row <#productID> ?productID ;
        <#productName> ?productName ;
        <#supplierID> ?supplierID ;
        <#categoryID> ?categoryID ;
        <#quantityPerUnit> ?quantityPerUnit ;
        <#unitPrice> ?unitPriceString ;
        <#unitsInStock> ?unitsInStockString ;
        <#unitsOnOrder> ?unitsOnOrderString ;
        <#reorderLevel> ?reorderLevelString ;
        <#discontinued> ?discontinuedString .

    BIND(uri(concat(str(?container), encode_for_uri(?productID), "/")) AS ?item)
    BIND(uri(concat(str(?item), "#this")) AS ?product)
    BIND(uri(concat(str($base), "suppliers/", encode_for_uri(?supplierID), "/#this")) AS ?supplier)
    BIND(uri(concat(str($base), "categories/", encode_for_uri(?categoryID), "/#this")) AS ?category)
    BIND (STRDT(?unitPriceString, xsd:float) AS ?unitPrice)
    BIND (STRDT(?unitsInStockString, xsd:integer) AS ?unitsInStock)
    BIND (STRDT(?unitsOnOrderString, xsd:integer) AS ?unitsOnOrder)
    BIND (STRDT(?reorderLevelString, xsd:integer) AS ?reorderLevel)
    BIND (STRDT(?discontinuedString, xsd:integer) AS ?discontinued)
}