PREFIX def:        <ns/default#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT
{
    ?item a def:Item ;
        sioc:has_container ?this ;
        dh:slug ?employeeID ;
        dct:title ?lastName ;
        foaf:primaryTopic ?employee .

    ?employee a schema:Person ;
        foaf:isPrimaryTopicOf ?item ;
        schema:identifier ?employeeID ;
        dct:title ?lastName ;
        schema:givenName ?firstName ;
        schema:familyName ?lastName ;
        schema:jobTitle ?title ;
        schema:honorificPrefix  ?titleOfCourtesy ;
        schema:birthDate ?birthDate;
        # ?hireDate
        schema:address ?postalAddress ;
        schema:telephone ?homePhone ;
        schema:telephone ?extension ;
        schema:description ?notes ;
        schema:sponsor ?reportsToEmployee .

    ?postalAddress a schema:PostalAddress ;
        schema:addressCountry ?country ;
        schema:addressLocality ?city ;
        schema:postalCode ?postalCode ;
        schema:streetAddress ?address ;
        schema:addressRegion ?region .
}
WHERE
{
    ?employee_row <#employeeID> ?employeeID ;
        <#lastName> ?lastName ;
        <#firstName> ?firstName ;
        <#title> ?title ;
        <#titleOfCourtesy> ?titleOfCourtesy ;
        <#birthDate> ?birthDateString ;
        <#hireDate> ?hireDateString ;
        <#address> ?address ;
        <#city> ?city ;
        <#postalCode> ?postalCode ;
        <#country> ?country ;
        <#homePhone> ?homePhone ;
        <#extension> ?extension ;
        <#photo> ?picture ;
        <#notes> ?notes .
    OPTIONAL {
        ?employee_row <#region> ?region
    }
    OPTIONAL {
        ?employee_row <#reportsTo> ?reportsTo
    }

    BIND(uri(concat(str(<employees/>), encode_for_uri(?employeeID), "/")) AS ?item)
    BIND(uri(concat(str(<employees/>), encode_for_uri(?employeeID), "/#this")) AS ?employee)
    BIND(uri(concat(str(<employees/>), encode_for_uri(?employeeID), "/#address")) AS ?postalAddress)
    BIND(strdt(substr(?birthDateString, 1, 10), xsd:date) AS ?birthDate)
    BIND(strdt(substr(?hireDateString, 1, 10), xsd:date) AS ?hireDate)
    BIND(uri(concat(str(<employees/>), encode_for_uri(?reportsTo), "/#this")) AS ?reportsToEmployee)
}
