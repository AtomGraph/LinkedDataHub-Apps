PREFIX def:        <ns/default#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    ?item a def:Item ;
        sioc:has_container ?this ;
        dh:slug ?shipperID ;
        dct:title ?companyName ;
        foaf:primaryTopic ?shipper .
    ?shipper a schema:Corporation ;
        foaf:isPrimaryTopicOf ?item ;
        dct:title ?companyName ;
        schema:legalName ?companyName ;
        schema:id ?shipperID ;
        schema:telephone ?phone .
}
WHERE
{
    BIND (BNODE() AS ?item)

    ?shipper_row <#shipperID> ?shipperID ;
        <#companyName> ?companyName ;
        <#phone> ?phone .

    BIND(uri(concat(str(<shippers/>), encode_for_uri(?shipperID), "/#this")) AS ?shipper)
}
