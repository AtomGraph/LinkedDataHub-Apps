PREFIX def:        <ns/default#>
PREFIX dom:        <ns/domain#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX dbo:        <http://dbpedia.org/ontology/>
PREFIX schema:     <https://schema.org/>
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
  ?item a def:Item ;
      sioc:has_container ?this ;
      dh:slug ?id ;
      dct:title ?name ;
      foaf:primaryTopic ?territory .
  ?territory a schema:City ;
      foaf:isPrimaryTopicOf ?item ;
      dct:title ?name ;
      dct:identifier ?territoryID;
      schema:containedInPlace ?region.
}
WHERE
{
  BIND (BNODE() AS ?item)

  ?territory <#territoryID> ?territoryID ;
             <#territoryDescription> ?name ;
             <#regionID> ?regionID .

  BIND(uri(concat(str(<regions/>), encode_for_uri(?regionID), "/#this")) AS ?region)
}
