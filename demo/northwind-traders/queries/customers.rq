PREFIX def:        <ns/default#>
PREFIX dom:        <ns/domain#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
  ?item a def:Item ;
      sioc:has_container ?this ;
      dh:slug ?id ;
      dct:title ?name ;
      foaf:primaryTopic ?customer .
  ?customer a schema:Corporation ;
      foaf:isPrimaryTopicOf ?item ;
      schema:id ?id ;
      schema:legalName ?name ;
      schema:contactPoint [ schema:telephone ?phone ;
                            schema:faxNumber ?fax ] ;
      schema:employee [ schema:name ?contactName ;
                        schema:roleName ?contactTitle ;
                        schema:telephone ?phone ;
                        schema:faxNumber ?fax ] ;
      schema:address [ schema:addressCountry ?country ;
                       schema:addressLocality ?city ;
                       schema:postalCode ?postalCode ;
                       schema:streetAddress ?address ;
                       schema:location [ schema:name ?region ]] .
}
WHERE
{
  BIND (BNODE() AS ?item)

  ?customer <#customerID> ?id ;
      <#companyName> ?name ;
      <#contactName> ?contactName;
      <#contactTitle> ?contactTitle;
      <#address> ?address;
      <#city> ?city;
      <#region> ?region;
      <#postalCode> ?postalCode;
      <#country> ?country;
      <#phone> ?phone;
      <#fax> ?fax.
}
