PREFIX def:        <ns/default#>
PREFIX dom:        <ns/domain#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
  ?item a def:Item ;
      sioc:has_container ?this ;
      dh:slug ?id ;
      dct:title ?name ;
      foaf:primaryTopic ?category .
  ?category a schema:ProductGroup ;
      foaf:isPrimaryTopicOf ?item ;
      dct:title ?name ;
      schema:id ?id ;
      schema:description ?description ;
      schema:image ?picture .
}
WHERE
{
  BIND (BNODE() AS ?item)

  ?category <#categoryID> ?id ;
            <#description> ?description ;
            <#categoryName> ?name ;
            <#picture> ?picture .
}
