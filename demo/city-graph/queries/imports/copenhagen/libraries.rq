PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX dbo:        <http://dbpedia.org/ontology/> 
PREFIX schema:     <https://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    GRAPH ?item
    {
        ?item a dh:Item ;
            sioc:has_container ?container ;
            dh:slug ?id ;
            dct:title ?name ;
            foaf:primaryTopic ?library .
        ?library a schema:Library ;
            geo:lat ?lat ;
            geo:long ?long ;
            dct:title ?name ;
            schema:streetAddress ?address ;
            schema:postalCode ?postcode ;
            dbo:district ?district ;
            foaf:page ?link ;
            foaf:mbox ?email ;
            dct:identifier ?id .
    }
}
WHERE
{
    ?library <#FID> ?id ;
        <#name> ?name ;
        <#address> ?address ;
        <#lat> ?lat_string ;
        <#postcode> ?postcode ;
        <#postdistrikt> ?district ;
        <#long> ?long_string . 

    OPTIONAL 
    {
        ?library <#email> ?emailString
        BIND (URI(CONCAT(STR(<mailto:>), ?emailString)) AS ?email)
    }
    OPTIONAL 
    {
        ?library <#link> ?linkString
        BIND (URI(?linkString) AS ?link)
    }

    BIND(uri(concat(str($base), "copenhagen/libraries/")) AS ?container)
    BIND(uri(concat(str(?container), encode_for_uri(?id), "/")) AS ?item)
    BIND(xsd:float(?lat_string) AS ?lat)
    BIND(xsd:float(?long_string) AS ?long)
}