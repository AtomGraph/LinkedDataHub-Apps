PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX dbo:        <http://dbpedia.org/ontology/> 
PREFIX schema:     <https://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    GRAPH ?item
    {
        ?item a dh:Item ;
            sioc:has_container ?container ;
            dh:slug ?id ;
            dct:title ?name ;
            foaf:primaryTopic ?sportCenter .

        ?sportCenter a schema:SportsActivityLocation ;
            geo:lat ?lat ;
            geo:long ?long ;
            dct:title ?name ;
            schema:streetAddress ?address ;
            schema:postalCode ?postcode ;
            foaf:mbox ?email ;
            foaf:page ?link ;
            dct:identifier ?id .
    }
}
WHERE
{
    ?sportCenter_row <#FID> ?id ;
        <#name> ?name ;
        <#address> ?address ;
        <#lat> ?lat_string ;
        <#postcode> ?postcode ;
        <#long> ?long_string .

    OPTIONAL {
        ?sportCenter_row <#email> ?emailString
        BIND (URI(CONCAT(STR(<mailto:>), ?emailString)) AS ?email)
    }
    OPTIONAL {
        ?sportCenter_row <#link> ?linkString
        BIND (URI(?linkString) AS ?link) 
    }

    BIND(uri(concat(str($base), "copenhagen/sports-centers/")) AS ?container)
    BIND(uri(concat(str(?container), encode_for_uri(?id), "/")) AS ?item)
    BIND(uri(concat(str(?item), "#this")) AS ?sportCenter)
    BIND(xsd:float(?lat_string) AS ?lat)
    BIND(xsd:float(?long_string) AS ?long)
}