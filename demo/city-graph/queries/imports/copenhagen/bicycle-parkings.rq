PREFIX ns:         <https://linkeddatahub.com/demo/city-graph/admin/ontologies/namespace/#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX dbo:        <http://dbpedia.org/ontology/> 
PREFIX schema:     <https://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    GRAPH ?item
    {
        ?item a dh:Item ;
            sioc:has_container ?container ;
            dh:slug ?id ;
            dct:title ?name ;
            foaf:primaryTopic ?bicyleParking .

        ?bicyleParking a ns:BicycleParking ;
            geo:lat ?lat ;
            geo:long ?long ;
            dct:title ?name ;
            dbo:city ?city ;
            schema:streetAddress ?address ;
            schema:maximumAttendeeCapacity ?spaces ;
            dct:identifier ?id .
    }
}
WHERE
{
    ?bicyleParking <#FID> ?id ;
        <#lat> ?lat_string ;
        <#long> ?long_string . 

    OPTIONAL 
    { ?bicyleParking <#name> ?name }
    OPTIONAL 
    { ?bicyleParking <#bydel> ?city }
    OPTIONAL 
    { ?bicyleParking <#vejnavn> ?address }
    OPTIONAL 
    { ?bicyleParking <#antal_pladser> ?spaces }

    BIND(uri(concat(str($base), "copenhagen/bicycle-parkings/")) AS ?container)
    BIND(uri(concat(str(?container), encode_for_uri(?id), "/")) AS ?item)
    BIND(xsd:float(?lat_string) AS ?lat)
    BIND(xsd:float(?long_string) AS ?long)

}