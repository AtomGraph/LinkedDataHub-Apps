PREFIX ns:         <https://linkeddatahub.com/demo/city-graph/admin/ontologies/namespace/#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:       <http://rdfs.org/sioc/ns#>

CONSTRUCT
{
    GRAPH ?item
    {
        ?item a dh:Item ;
            sioc:has_container ?container ;
            dh:slug ?id ;
            dct:title ?name ;
            foaf:primaryTopic ?charger .

        ?charger a ns:ChargingStation ;
            geo:lat ?lat ;
            geo:long ?long ;
            schema:streetAddress ?address ;
            dct:title ?name ;
            schema:additionalProperty ?chargeType ;
            schema:maximumAttendeeCapacity ?spaces ;
            schema:serviceOperator ?operator ;
            schema:comment ?information ;
            dct:identifier ?id .
    }
}
WHERE
{
    ?charger_row <#FID> ?id ;
        <#lat> ?lat_string ;
        <#long> ?long_string . 

    OPTIONAL 
    { ?charger_row <#name> ?name }
    OPTIONAL 
    { ?charger_row <#chargeType> ?chargeType }
    OPTIONAL 
    { ?charger_row <#address> ?address }
    OPTIONAL 
    { ?charger_row <#antal_udtag> ?spaces }
    OPTIONAL 
    { ?charger_row <#operator> ?operator }
    OPTIONAL 
    { ?charger_row <#bem> ?information }

    BIND(uri(concat(str($base), "copenhagen/charging-stations/")) AS ?container)
    BIND(uri(concat(str(?container), encode_for_uri(?id), "/")) AS ?item)
    BIND(uri(concat(str(?item), "#this")) AS ?charger)
    BIND(xsd:float(?lat_string) AS ?lat)
    BIND(xsd:float(?long_string) AS ?long)
}